/PREP7 !start preprocessor


X_COORD = ARG2
W_Divisions = ARG4
!MakeUseOfSymmetry = ARG3 !'True' == 'Symmetrie'
GeometryFolder(1) = '%GeometryFolderCommon(1)%\%SleeperModel(1)%'

!This is the main file for creating the KLP (rectangular) sleeper. 
!Necessary for this file are loaded parameters 
*IF, ARG1, EQ, 201, THEN
	/INPUT, parameters_201, f, '%GeometryFolder(1)%' , 0, 1
*ELSEIF, ARG1, EQ, 202
	/INPUT, parameters_202, f, '%GeometryFolder(1)%' , 0, 1
*ELSE
	/INPUT, parameters_201, f, '%GeometryFolder(1)%' , 0, 1 !Default
*ENDIF


*GET, CS_max , CDSY, 0, NUM, MAX
CS_SleeperLocal = max(11,CS_max + 1)
LOCAL, CS_SleeperLocal, CART, X_COORD, -S_Halve_Hoogte, 0, 0, 0,	0, ,  ! 
CS_WapeningLocal = max(12,CS_max + 2)
LOCAL, CS_WapeningLocal, CART, X_COORD + W_x, W_y, 0, 0, 0,	0, ,  ! 
*DEL, CS_max

real, realSolids

!-----------------------------
!Create halve Rechthoek / dwarsligger
CSYS, CS_SleeperLocal !X-location is based on the CS_SleeperLocal x-coordinate.

*USE, '%GeometryFolder(1)%\Subparts\createSleeperBase.MAC', CS_SleeperLocal, ARG3
*USE, '%GeometryFolder(1)%\Subparts\createReinforcement.MAC', CS_WapeningLocal, W_Divisions, ARG3

!----------------------------------------------------------------------
!--------- Merge reinforcement with the base --------------------------
!----------------------------------------------------------------------
CMSEL, S, Wapening_with_block_A
CMSEL, A, Dwarsligger_A
AGLUE, ALL

*GET, MaxLine, LINE, 0 , NUM, MAXD
!Concat lines for meshing (this are the radial lines that make up the wapening-section
LSEL, S, LINE, , Line_0 + 1, MaxLine
LCCAT, Line_0 + 2, Line_0 + 3
LCCAT, Line_0 + 4, Line_0 + 5
LCCAT, Line_0 + 6, Line_0 + 7
LCCAT, Line_0 + 8, Line_0 + 1

LCCAT, Line_0 + 17, Line_0 + 24
LCCAT, Line_0 + 18, Line_0 + 19
LCCAT, Line_0 + 20, Line_0 + 21
LCCAT, Line_0 + 22, Line_0 + 23

*IF, ARG3, NE, 'true', THEN
	LCCAT, Line_0 + 44, Line_0 + 51
	LCCAT, Line_0 + 45, Line_0 + 46
	LCCAT, Line_0 + 47, Line_0 + 48
	LCCAT, Line_0 + 49, Line_0 + 50

	LCCAT, Line_0 + 33, Line_0 + 40
	LCCAT, Line_0 + 34, Line_0 + 35
	LCCAT, Line_0 + 36, Line_0 + 37
	LCCAT, Line_0 + 38, Line_0 + 39
*ENDIF

*USE, '%GeometryFolderCommon(1)%\renumberEntities.MAC' !Renumber to be sure that there are no gaps

X_LOC_MIN = -S_Halve_Breedte
X_LOC_MAX = S_Halve_Breedte

ALLSEL
LSEL, S, LOC, Z, 0
LSEL, R, LOC, X, X_LOC_MIN, X_LOC_MAX
LSEL, R, LOC, Y, -S_Halve_Hoogte, S_Halve_Hoogte
CM, Z_plane, LINE
!Concatenate the lines from the middle block
LSEL, R, LOC, Y, S_block_height
LSEL, R, LOC, X, 0, X_LOC_MAX
LCCAT, ALL

CMSEL, S, Z_plane
LSEL, R, LOC, Y, -S_block_height
LSEL, R, LOC, X, 0, X_LOC_MAX
LCCAT, ALL
	
*IF, ARG3, NE, 'true', THEN !Lijnen in het negatieve x-vlak
	CMSEL, S, Z_plane
	LSEL, R, LOC, Y, S_block_height
	LSEL, R, LOC, X, X_LOC_MIN, 0
	LCCAT, ALL

	CMSEL, S, Z_plane
	LSEL, R, LOC, Y, -S_block_height
	LSEL, R, LOC, X, X_LOC_MIN, 0
	LCCAT, ALL
*ENDIF	
	
ASEL, S, LOC, Z, 0
MSHKEY, 1 !Mapped meshing
ESIZE, (OB_size_width/2)/W_Divisions
AMESH, ALL

ALLSEL
LSEL, S, LCCA
LDEL, ALL
ALLSEL

!----------------------------------------------------------------------
!--------- Extrude ----------------------------------------------------
!----------------------------------------------------------------------
*USE, '%GeometryFolder(1)%\extrude.MAC'
ALLSEL

!----------------------------------------------------------------------
!-------Create components ---------------------------------------------
!----------------------------------------------------------------------
!TODO, remove duplication between this script and the non-simple-sleeper.

!wapening
CSYS, CS_SleeperLocal
Midpoint_X = S_Halve_Breedte - W_Dekking_x - W_Straal !=W_X)
Midpoint_Y = S_Halve_Hoogte -  W_Dekking_y - W_Straal 

VSEL, S, LOC, X, Midpoint_X - W_Straal_corrected, 			Midpoint_X + W_Straal_corrected
VSEL, R, LOC, Y, Midpoint_Y - W_Straal_corrected, 			Midpoint_Y + W_Straal_corrected
VSEL, R, LOC, Z, -W_Halve_Lengte, W_Halve_Lengte
CM, Wapening_1_V, VOLU
VSEL, S, LOC, X, Midpoint_X - W_Straal_corrected, 			Midpoint_X + W_Straal_corrected
VSEL, R, LOC, Y, -Midpoint_Y - W_Straal_corrected, 			-Midpoint_Y + W_Straal_corrected
VSEL, R, LOC, Z, -W_Halve_Lengte, W_Halve_Lengte
CM, Wapening_2_V, VOLU

VSEL, S, LOC, X, -Midpoint_X - W_Straal_corrected, 			-Midpoint_X + W_Straal_corrected
VSEL, R, LOC, Y, Midpoint_Y - W_Straal_corrected, 			Midpoint_Y + W_Straal_corrected
VSEL, R, LOC, Z, -W_Halve_Lengte, W_Halve_Lengte
CM, Wapening_3_V, VOLU
CM, Wapening_V_RB, VOLU
VSEL, S, LOC, X, -Midpoint_X - W_Straal_corrected, 			-Midpoint_X + W_Straal_corrected
VSEL, R, LOC, Y, -Midpoint_Y - W_Straal_corrected, 			-Midpoint_Y + W_Straal_corrected
VSEL, R, LOC, Z, -W_Halve_Lengte, W_Halve_Lengte
CM, Wapening_4_V, VOLU
CM, Wapening_V_RO, VOLU

ALLSEL
CMSEL, S, Wapening_V
CMSEL, A, Wapening_1_V
CMSEL, A, Wapening_2_V
CMSEL, A, Wapening_3_V
CMSEL, A, Wapening_4_V
CM, Wapening_V, VOLU
ESLV, S
EMODIF, ALL, MAT, Materiaal_W
!VATT, Materiaal_W

!Dwarsligger
ALLSEL
*GET, DoesRughellingplaatExists, COMP, Rughellingplaten, TYPE
*GET, DoesRailExists, COMP, Rail_V, TYPE
*GET, DoesRailpadExists, COMP, Railpads_V, TYPE
*IF, DoesRughellingplaatExists, GT, 0, AND, DoesRailExists, GT, 0, THEN !Unselect only if they exist
	CMSEL, U, Rughellingplaten
	CMSEL, U, Rail_V
*ENDIF
*IF, DoesRailpadExists, GT, 0, THEN !Unselect only if they exist
	CMSEL, U, Railpads_V
*ENDIF

*IF, Materiaal_S, NE, Materiaal_W, THEN
	CMSEL, U, Wapening_V
*ENDIF	
VSEL, R, LOC, X, -S_Halve_Breedte, S_Halve_Breedte
CM, Kunststof_V_Locaal, VOLU

CMSEL, S, Kunststof_V
CMSEL, A, Kunststof_V_Locaal
CM, Kunststof_V, VOLU

ALLSEL
*USE, '%GeometryFolderCommon(1)%\renumberEntities.MAC' !Renumber to be sure that there are no gaps

ALLSEL


*DEL, DoesRughellingplaatExists
*DEL, DoesRailExists
*DEL, MaxLine
*DEL, Midpoint_X
*DEL, Midpoint_Y
*DEL, X_LOC_MIN
*DEL, X_LOC_MAX
*DEL, X_COORD

